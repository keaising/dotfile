#!/usr/bin/env fish

function ins_nvim
    rm -rf ~/.local/bin/nvim
    sudo rm -f /usr/local/bin/nvim
    mkdir -p ~/.local/bin

    set -l WORKDIR "$(mktemp -d)"
    cd $WORKDIR

    set -l VERSION nightly
    [ $argv[1] ] && set -l VERSION $argv[1]

    set -l NVIM_FILE nvim-linux-x86_64
    [ (uname) != Linux ] && set -l NVIM_FILE nvim-macos-arm64
    wget https://github.com/neovim/neovim/releases/download/$VERSION/$NVIM_FILE.tar.gz
    tar -zxf $NVIM_FILE.tar.gz
    rm $NVIM_FILE.tar.gz
    mv $NVIM_FILE ~/.local/bin/nvim

    rm -r $WORKDIR

    sudo ln -s ~/.local/bin/nvim/bin/nvim /usr/local/bin/nvim
    nvim --version
end

function ins_gotools
    set -lx GOPROXY "https://goproxy.cn"
    set -lx GO111MODULE on

    go version
    echo $PWD
    # go env

    set -l REPOS \
        golang.org/x/tools/gopls \
        github.com/go-delve/delve/cmd/dlv \
        github.com/haya14busa/goplay/cmd/goplay \
        github.com/fatih/gomodifytags \
        github.com/josharian/impl \
        github.com/cweill/gotests/... \
        honnef.co/go/tools/cmd/staticcheck \
        github.com/golangci/golangci-lint/cmd/golangci-lint \
        github.com/rinchsan/gosimports/cmd/gosimports \
        github.com/segmentio/golines \
        github.com/kisielk/errcheck \
        golang.org/x/vuln/cmd/govulncheck

    for repo in $REPOS
        set -l run "go install $repo@latest"
        echo $run
        eval $run
    end

end

function main
    switch $argv[1]
        case nvim
            ins_nvim $argv[2..-1]
        case go gotools
            ins_gotools
    end
end

main $argv
